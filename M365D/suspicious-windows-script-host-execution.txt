// Execution of Windows Script Host (.vbs, .vbe, .js, .jse, etc.) from user Downloads/temp folder (i.e. opened from zip archive) or from web browser.
DeviceProcessEvents 
| where FileName has_any("wscript.exe", "cscript.exe")
| where 
(
    InitiatingProcessFileName has_any ("explorer.exe", "7zFM.exe") and
    (
        ProcessCommandLine matches regex @"(?i)\\Users\\[^\\]+\\Downloads\\" or
        ProcessCommandLine matches regex @"(?i)\\Users\\[^\\]+\\AppData\\Local\\Temp\\[^\\]+\\.+\.zip"
    )
    or
    (
        InitiatingProcessFileName has_any ("msedge.exe", "chrome.exe", "firefox.exe")
    )
)